buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.7.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'com.gradleup.shadow' version '8.3.8' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'architectury-plugin'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'com.gradleup.shadow'

    base {
        archivesName = "$rootProject.archives_name-$project.name-$rootProject.minecraft_version"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url 'https://maven.parchmentmc.org' }
    }

    configurations {
        // Files in this configuration will be bundled into your mod using the Shadow plugin.
        // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
        shadowBundle {
            canBeResolved = true
            canBeConsumed = false
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_mappings}@zip")
        }

        shadowBundle "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.kotlin_version}"
        shadowBundle "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${rootProject.kotlin_version}"
        shadowBundle "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${rootProject.kotlin_version}"
    }

    kotlin {
        jvmToolchain(21)
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        // withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    jar {
        manifest {
            attributes([
                'Specification-Title'     : rootProject.archives_name,
                'Specification-Vendor'    : 'luavixen',
                'Specification-Version'   : '1',
                'Implementation-Title'    : 'Loading Backgrounds',
                'Implementation-Version'  : rootProject.mod_version,
                'Implementation-Vendor'   : 'luavixen',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    processResources {
        filesMatching(['fabric.mod.json', 'META-INF/mods.toml', 'META-INF/*.mods.toml']) {
            expand([
                'version': rootProject.mod_version,
                'minecraft_version': rootProject.minecraft_version,
                'minecraft_version_min': rootProject.minecraft_version_min,
                'minecraft_version_max': rootProject.minecraft_version_max,
                'minecraft_version_semver': rootProject.minecraft_version_semver,
            ])
        }
    }

    tasks.register('proguard', proguard.gradle.ProGuardTask) {
        dependsOn remapJar

        def inJar = remapJar.archiveFile.get()
        def outJar = inJar.asFile.absolutePath.replace('.jar', '-bundle.jar')

        injars inJar, filter: '!**.kotlin_builtins,!**.kotlin_module'
        outjars outJar

        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'

        libraryjars provider {
            configurations.runtimeClasspath.files.findAll { it.exists() }
        }

        dontoptimize

        keep 'class dev.foxgirl.mc.** { *; }'

        keepattributes '**'

        repackageclasses 'dev.foxgirl.bundled'

        keepclassmembernames 'class kotlin.** { *; }'

        dontwarn 'net.minecraft.**'
        dontwarn 'dev.foxgirl.mc.**'

        dontnote 'kotlin.**'
        dontnote 'org.intellij.**'
    }

    tasks.build {
        dependsOn proguard
    }
}
